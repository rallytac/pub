plugins {
    alias(libs.plugins.android.application)
}

ext.engageVersion = "1.251.9091"
ext.useEngageAar = true

// Apply RTS-specific configuration
try {
    apply from: "ext/rts.gradle"
}
catch (ignored) {
}

android {
    namespace 'com.rallytac.engageandroid'
    compileSdk 34

    flavorDimensions "version"

    productFlavors {
        // This is the Rally Tactical Systems "reference" flavor
        reference {
            dimension "version"
            applicationId "com.rallytac.engageandroid.reference"
        }
    }

    // Add additional OEM ext if any
    try {
        apply from: "ext/flavors.gradle"
    }
    catch (ignored) {
    }

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        applicationId "com.rallytac.engageandroid"
        minSdk 24
        targetSdk 34
        versionCode 1337
        versionName "1337"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.all { output ->
            def apkBaseName = variant.productFlavors.get(0).name
            def SEP = "_"
            def buildType = variant.buildType.name
            def version = variant.versionCode
            def newApkName = apkBaseName + SEP + version + SEP + buildType + ".apk"

            outputFileName = new File(newApkName)
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit

    implementation libs.zxing.android.embedded

    if(useEngageAar) {
        implementation files("libs/engage-engine-release-${engageVersion}.aar")
    }
}